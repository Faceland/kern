plugins {
    id "com.github.hierynomus.license" version "0.11.0"
    id "com.github.johnrengelman.shadow" version "1.2.0"
    id "java"
    id "maven"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'licenseFormat', 'check', 'build'

group = "com.tealcube.minecraft.bukkit"
archivesBaseName = "kern"
version = "1.0.0-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://maven.sk89q.com/repo/"
    }
    maven {
        url "http://repo.franga2000.com/artifactory/public"
    }
    maven {
        url "http://repository-topplethenun.forge.cloudbees.com/snapshot/"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
}

artifacts {
    archives shadowJar
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile "com.google.guava:guava:18.0"
    compile "com.google.code.gson:gson:2.3.1"
    compile "com.typesafe:config:1.2.1"
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile "org.bukkit:bukkit:1.8-R0.1-SNAPSHOT"
    compile "com.sk89q:squirrelid:0.1.0"
    compile "mkremins:fanciful:0.3.1"
    compile "org.flywaydb:flyway-core:3.0"
    compile "net.objecthunter:exp4j:0.4.0.ALPHA-3"
    compile "se.ranzdo.bukkit:methodcommand:0.2-SNAPSHOT"
}

publishing {
    publications {
        java(MavenPublication) {
            from components.java
            artifactId = "kern"

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
        shadow(MavenPublication) {
            from components.shadow
            artifactId = "kern-all"

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
    repositories {
        maven {
            url "http://tealcube.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "TealCube"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}

shadowJar {
    baseName = "kern-all"
    classifier = ""

    dependencies {
        exclude("org.bukkit:bukkit:1.8-R0.1-SNAPSHOT")
    }

    relocate "com.google", "com.tealcube.minecraft.bukkit.kern.shade.google"
    relocate "com.typesafe", "com.tealcube.minecraft.bukkit.kern.typesafe"
    relocate "org.apache", "com.tealcube.minecraft.bukkit.kern.apache"
    relocate "org.aopalliance", "com.tealcube.minecraft.bukkit.kern.aopalliance"
    relocate "mkremins.fanciful", "com.tealcube.minecraft.bukkit.kern.fanciful"
    relocate "net.amoebaman", "com.tealcube.minecraft.bukkit.kern.amoebaman"
    relocate "net.objecthunter", "com.tealcube.minecraft.bukkit.kern.objecthunter"
    relocate "se.ranzdo.bukkit.methodcommand", "com.tealcube.minecraft.bukkit.kern.methodcommand"
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}