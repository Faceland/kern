plugins {
    id "java"
    id "com.github.hierynomus.license" version "0.11.0"
    id "pl.allegro.tech.build.axion-release" version "1.2.4"
    id "com.github.johnrengelman.shadow" version "1.2.1"
    id "signing"
}

sourceCompatibility = 1.7

scmVersion {
    tag {
        prefix = project.name
    }
    createReleaseCommit true
    releaseCommitMessage { version, position -> "[ci skip] release version: " + version}
}

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://maven.sk89q.com/repo/"
    }
    maven {
        url "http://repo.franga2000.com/artifactory/public"
    }
    maven {
        url "http://tealcube.com/nexus/content/groups/public/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
}

artifacts {
    archives shadowJar
    archives sourcesJar
    archives javadocJar
}

dependencies {
    compile "com.google.guava:guava:18.0"
    compile "com.google.code.gson:gson:2.3.1"
    compile "com.typesafe:config:1.2.1"
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile("org.spigotmc:spigot-api:1.8.3-R0.1-SNAPSHOT") {
        exclude module: "bungeecord-chat"
    }
    compile "com.sk89q:squirrelid:0.1.0"
    compile "com.tealcube.minecraft.bukkit:fanciful:0.1.0"
    compile "net.objecthunter:exp4j:0.4.0.ALPHA-3"
    compile "com.tealcube.se.ranzdo.bukkit:methodcommand:0.1.0"
}

shadowJar {
    dependencies {
        exclude("org.spigotmc:spigot-api")
    }

    relocate "com.google", "com.tealcube.minecraft.bukkit.kern.shade.google"
    relocate "com.typesafe", "com.tealcube.minecraft.bukkit.kern.typesafe"
    relocate "org.apache", "com.tealcube.minecraft.bukkit.kern.apache"
    relocate "org.aopalliance", "com.tealcube.minecraft.bukkit.kern.aopalliance"
    relocate "mkremins.fanciful", "com.tealcube.minecraft.bukkit.kern.fanciful"
    relocate "net.amoebaman", "com.tealcube.minecraft.bukkit.kern.amoebaman"
    relocate "net.objecthunter", "com.tealcube.minecraft.bukkit.kern.objecthunter"
}

license {
    header file('HEADER')
    ignoreFailures false
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}
