plugins {
    id "com.github.johnrengelman.shadow" version "1.2.0"
    id "java"
    id "maven"
    id "maven-publish"
    id "wrapper"
}

sourceCompatibility = "1.7"

defaultTasks 'clean', 'check', 'build'

group = "org.nunnerycode"
archivesBaseName = "kern"
version = "0.0.4-SNAPSHOT"

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
    maven {
        url "http://maven.sk89q.com/repo/"
    }
    maven {
        url "https://raw.github.com/mkremins/fanciful/mvn-repo/"
    }
}

artifacts {
    archives shadowJar
}

dependencies {
    compile "com.google.inject:guice:4.0-beta5"
    compile "org.jooq:jooq:3.5.0"
    compile "org.jooq:jooq-meta:3.5.0"
    compile "org.jooq:jooq-codegen:3.5.0"
    compile "com.google.guava:guava:18.0"
    compile "com.google.code.gson:gson:2.3.1"
    compile "com.typesafe:config:1.2.1"
    compile "org.apache.commons:commons-lang3:3.3.2"
    compile "org.bukkit:bukkit:1.8-R0.1-SNAPSHOT"
    compile "com.sk89q:intake:3.1.1-SNAPSHOT"
    compile "com.sk89q:squirrelid:0.1.0"
    compile "mkremins:fanciful:0.2.0-SNAPSHOT"
    compile "org.flywaydb:flyway-core:3.1"
    compile "net.objecthunter:exp4j:0.4.0.ALPHA-3"
}

publishing {
    publications {
        shadow(MavenPublication) {
            from components.shadow
            artifactId = "kern-all"
        }
    }
    repositories {
        maven {
            url "http://repo-topplethenun.rhcloud.com/nexus/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}/"
            name "nunnerycode-repo"
            credentials {
                username System.getenv("nexusUsername") ?: nexusUsername
                password System.getenv("nexusPassword") ?: nexusPassword
            }
        }
    }
}

shadowJar {
    baseName = "kern-all"
    classifier = ""

    dependencies {
        exclude("org.bukkit:bukkit:1.8-R0.1-SNAPSHOT")
    }

    relocate "com.google", "org.nunnerycode.kern.shade.google"
    relocate "org.jooq", "org.nunnerycode.kern.shade.jooq"
    relocate "com.typesafe", "org.nunnerycode.kern.typesafe"
    relocate "org.apache", "org.nunnerycode.kern.apache"
    relocate "org.aopalliance", "org.nunnerycode.kern.aopalliance"
    relocate "javax.inject", "org.nunnerycode.kern.inject"
    relocate "com.sk89q.intake", "org.nunnerycode.kern.intake"
    relocate "mkremins.fanciful", "org.nunnerycode.kern.fanciful"
    relocate "net.amoebaman", "org.nunnerycode.kern.amoebaman"
    relocate "org.flywaydb", "org.nunnerycode.kern.flywaydb"
    relocate "net.objecthunter", "org.nunnerycode.kern.objecthunter"
}